SUBROUTINE SATOBFREQ_BYNAM(KREADMODE,CDSERIES,FREQ,ID_FREQ)
USE PARKIND1  ,ONLY : JPIM   ,JPRD
USE YOMHOOK   ,ONLY : LHOOK  ,DR_HOOK

!**** SUBROUTINE SATOBFREQ_BYNAM - CHANNEL ID FROM CHANNEL FREQUENCY FOR SATOB
!
!          CHRISTOPHE PAYAN, METEO-FRANCE          12/FEB/2010
!
!     PURPOSE
!     -------
!     
!     WITH THE INTRODUCTION OF SATOBS FROM SEVERAL CHANNELS WITHIN THE SAME BAND 
!     IT GETS NECESSARY TO DISTINGUISH THESE FREQUENCIES WITHIN THE COMPUTATIONAL
!     METHOD. THEREFORE A FREQUENCY ID FOR EACH BAND IS CHOSEN. 
!     THE FREQUENCY ID IS DETERMINED IN THIS ROUTINE FOR EACH SATELLITE HAVING MORE 
!     ONE FREQUENCY PER BAND (COMPUTATIONAL METHOD). IT STARTS WITH METEOSAT SECOND 
!     GENERATION (MSG) AND GOES-N, GOES-M SERIES.
!
!     INSPIRED FROM SATOBFREQ CALLED BY SUBROUTINES bufr2odb_satob (odb/bufr2odb) AND buxtract (obstat/src)
!     SATOBFREQ_BYNAM ALLOWS TO MANAGE FREQUENCIES BY SATELLITE SERIES BY NAMELIST.
!     A SERIES IS A LIST OF SATELLITES HAVING SAME CHANNEL FREQUENCIES, FIXED BY USERS.
!     WE TRY TO LINK THESE SUBJECTIVE LISTS WITH COMMON NAMES OF SERIES AS MSG OR GOES-M.
!     THIS SHOULD BE MORE FLEXIBLE THAN SATOBFREQ, NEW SATELLITES MAY BE MANAGED BY NAMELIST.
!
!     INTERFACE
!     ---------
!
!     ***CALL*SATOBFREQ_BYNAM(KREADMODE,CDSERIES,FREQ,ID_FREQ)
!     WHERE KREADMODE= 0 INITIALISATION MADE ,1 WO NAMELIST ,2 WITH NAMELIST ,OTHER CALL ABOR1
!           CDSERIES = SATELLITE SERIES IDENTIFIER
!           FREQ     = CHANNEL FREQUENCY 
!           ID_FREQ  = ID (NUMBER) OF THE FREQUENCY WITHIN THE BAND
!
!     SATOBFREQ_BYNAM IS CALLED BY SUBROUTINE bator_decodbufr_mod (odb/pandor/module)
!
!*    METHOD
!     ------
!     
!     THE CHANNEL FREQUENCY IS COMPARED WITH THE KNOWN VALUES. IF A CHANNEL FREQUENCY 
!     CAN NOT BE SORTED, THE ID_FREQ KEEPS ZERO AND THIS CHANNEL WILL BE BLACKLISTED BY
!     THE 'notin' COMMAND IN THE BLACKLIST.
!
!     KNOWN SERIES:
!       'MSG'      :    MET8,9          SATIDS:  55, 56
!       'GOES-M'   :    GOES10,11,12,13,14    SATIDS: 254,255,256,257,258
!       'GOES-P'   :    GOES15          SATID:  259
!       'HTG'      :    HIMAWARI8,9     SATIDS: 173, 174
!       'GOES-R'   :    GOES16,17,18,19 SATID:  270, 271, 272, 273
!
!*    EXTERNALS
!     ---------

!**   MODIFICATIONS
!     -------------
!     17/11/2011  C. Payan MF: GOES-M and GOES-N grouped in GOES-M, same characteristics
!                   and for saving one rank, GOES-P added, base 37t1_bf.03 and 38_main
!     27/11/2018  C. Payan MF: GOES-P, HTG, GOES-R added, base 46_main 

IMPLICIT NONE

CHARACTER(LEN=*)   ,INTENT(IN)    :: CDSERIES
REAL(KIND=JPRD)    ,INTENT(IN)    :: FREQ                ! Hz
INTEGER(KIND=JPIM) ,INTENT(OUT)   :: ID_FREQ
INTEGER(KIND=JPIM) ,INTENT(INOUT) :: KREADMODE

INTEGER(KIND=JPIM) ,PARAMETER     :: JPMXSERIES=10 ,JPMXFREQ=15 ,JPMXWRITE=50
REAL(KIND=JPRD)    ,PARAMETER     :: epsilon=0.00001D+15 ! Hz

INTEGER(KIND=JPIM)                :: xx ,jx
INTEGER(KIND=JPIM)                :: INAM
REAL(KIND=JPRD)                   :: ZCLUM
REAL(KIND=JPRD) :: ZHOOK_HANDLE

TYPE SATOBFREQ_MAP
  CHARACTER(LEN=10)                    :: CLSERIES_MAP
  REAL(KIND=JPRD)    ,DIMENSION(1:JPMXFREQ)  :: ZFREQ_MAP
  INTEGER(KIND=JPIM) ,DIMENSION(1:JPMXFREQ)  :: IFREQ_MAP
  CHARACTER(LEN=15)  ,DIMENSION(1:JPMXFREQ)  :: CLABEL
END TYPE SATOBFREQ_MAP

TYPE(SATOBFREQ_MAP)  ,DIMENSION(1:JPMXSERIES) ,SAVE :: TS_SERIES
INTEGER(KIND=JPIM)   ,SAVE :: JXSERIES ,JXFREQ(1:JPMXFREQ)
INTEGER(KIND=JPIM)   ,SAVE :: IOUT ,IWRITE

CHARACTER(LEN=180)                           :: CLFMT

NAMELIST / NAMSATFREQ / TS_SERIES

#include "abor1.intfb.h"

!--------------------------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('SATOBFREQ_BYNAM',0,ZHOOK_HANDLE)

IF (ANY(KREADMODE==(/1,2/))) THEN
! Initialization in the first call (kreadmode in (1,2))

  ! Logical unit standard output 
  IOUT=6

  ! Light velocity
  ZCLUM=299792458._JPRD

  ! TS_SERIES
  DO xx=1,10
    TS_SERIES(xx) = SATOBFREQ_MAP('MAP_TBD',-9._JPRD,-9_JPIM,'LABEL_TBD')
  ENDDO

  TS_SERIES(1)%CLSERIES_MAP='MSG'
  TS_SERIES(1)%ZFREQ_MAP(1:8)=(/0.4721142137D15,0.3997233D15,0.3701142000D15,0.479668D14 &
& ,0.407881D14,0.344589D14,0.310344D14,0.277586D14/)
  TS_SERIES(1)%IFREQ_MAP(1:8)=(/1,3,2,1,2,1,2,3/)
  ! digit in CLABEL must be equal to IFREQ_MAP (see [ifs|arp]/var/suamv.F90)  
  TS_SERIES(1)%CLABEL(1:8)=(/'VIS1','VIS3','VIS2',' WV1',' WV2',' IR1',' IR2',' IR3'/)

  TS_SERIES(2)%CLSERIES_MAP='GOES-M'
  TS_SERIES(2)%ZFREQ_MAP(1:7)=(/0.461538D15,0.405405D14,0.428571D14,0.441176D14,0.280374D14,0.768699D14,0.461219D14/)
  TS_SERIES(2)%IFREQ_MAP(1:7)=(/1,1,2,3,1,2,3/)
  TS_SERIES(2)%CLABEL(1:7)=(/'VIS1',' WV1',' WV2',' WV3',' IR1',' IR2',' WV3'/)

  TS_SERIES(3)%CLSERIES_MAP='GOES-P'
  TS_SERIES(3)%ZFREQ_MAP(1:6)=(/0.461538D15,0.405405D14,0.428571D14,0.461219D14,0.280374D14,0.768699D14/)
  TS_SERIES(3)%IFREQ_MAP(1:6)=(/1,1,2,3,1,2/)
  TS_SERIES(3)%CLABEL(1:6)=(/'VIS1',' WV1',' WV2',' WV3',' IR1',' IR2'/)
  
  TS_SERIES(4)%CLSERIES_MAP='HTG'
  TS_SERIES(4)%ZFREQ_MAP(1:5)=(/0.46842570D15,0.41067400D14,0.43448100D14,0.48353600D14,0.28826100D14/)
  TS_SERIES(4)%IFREQ_MAP(1:5)=(/1,1,2,3,1/)
  TS_SERIES(4)%CLABEL(1:5)=(/'VIS1',' WV1',' WV2',' WV3',' IR1'/)

  TS_SERIES(5)%CLSERIES_MAP='GOES-R'
  TS_SERIES(5)%ZFREQ_MAP(1:6)=(/0.4684257D+15,0.408437D+14,0.431356D+14,0.484317D+14,0.267672D+14,0.768699D+14/)
  TS_SERIES(5)%IFREQ_MAP(1:6)=(/1,1,2,3,1,2/)
  TS_SERIES(5)%CLABEL(1:6)=(/'VIS1',' WV1',' WV2',' WV3',' IR1',' IR2'/)

  KREADMODE=KREADMODE-1

  ! If allowed, update by namelist
  IF (KREADMODE==1) THEN
    INAM=14
    OPEN (INAM,FILE='NAMELIST',STATUS='OLD',FORM='FORMATTED',ERR=901)
    READ (INAM,NAMSATFREQ,ERR=902,END=903)
901 CONTINUE
    CLOSE(INAM)
    KREADMODE=KREADMODE-1
  ENDIF

  ! Now all is initialized, we can fix JXSERIES and JXFREQ
  DO xx=1,JPMXSERIES
    IF (trim(TS_SERIES(xx)%CLSERIES_MAP)=='MAP_TBD') EXIT
    DO jx=1,JPMXFREQ
      IF (TS_SERIES(xx)%ZFREQ_MAP(jx)==-9_JPRD) EXIT
    ENDDO
    JXFREQ(xx)=jx-1
  ENDDO
  JXSERIES=xx-1
  
  ! Prints for being sure to know what we do
  WRITE(IOUT,*)'SATOBFREQ_BYNAM: satellite series and frequencies taken into account:'
  DO xx=1,JXSERIES
    WRITE(IOUT,*)trim(TS_SERIES(xx)%CLSERIES_MAP),':'
    WRITE(CLFMT,FMT="('(1X,''  Freq (Hz):'',',I2,'(E18.10,'',''),/&
                  &    ,1X,''WL (m*1e-6):'',',I2,'(F18.1 ,'',''),/&
                  &    ,1X,''     FreqId:'',',I2,'(I18   ,'',''),/&
                  &    ,1X,''LabelFreqId:'',',I2,'(A18   ,'','')   )')") &
                  & MAX(1,JXFREQ(xx)),MAX(1,JXFREQ(xx)),MAX(1,JXFREQ(xx)),MAX(1,JXFREQ(xx))
    WRITE(IOUT,FMT=CLFMT)TS_SERIES(xx)%ZFREQ_MAP(1:JXFREQ(xx))       &
                      & ,NINT(ZCLUM/TS_SERIES(xx)%ZFREQ_MAP(1:JXFREQ(xx))*1e7)/10._JPRD &
                      & ,TS_SERIES(xx)%IFREQ_MAP(1:JXFREQ(xx))       &
                      & ,(trim(TS_SERIES(xx)%CLABEL(jx)),jx=1,jxfreq(xx))
  ENDDO

ELSEIF(KREADMODE/=0) THEN
  CALL ABOR1('SATOBFREQ_BYNAM: KREADMODE NOTIN (0,1,2)')
ENDIF

ID_FREQ=0

IF (CDSERIES/='TBD') THEN
  FreqToId: DO xx=1,JXSERIES
    IF (CDSERIES==trim(TS_SERIES(xx)%CLSERIES_MAP)) THEN
      DO jx=1,JXFREQ(xx)
        IF (FREQ.GT.TS_SERIES(xx)%ZFREQ_MAP(jx)-epsilon.AND.FREQ.LT.TS_SERIES(xx)%ZFREQ_MAP(jx)+epsilon) THEN
          ID_FREQ=TS_SERIES(xx)%IFREQ_MAP(jx)
          EXIT FreqToId
        ENDIF
      ENDDO  
    ENDIF
  ENDDO FreqToId
  IF (ID_FREQ==0.AND.IWRITE.LT.JPMXWRITE) THEN
    WRITE(IOUT,FMT='(3A,E18.10)') '%E SATOBFREQ_BYNAM: channel frequency for ',CDSERIES,' has not been recognized, freq=',FREQ
    !-- Avoids to pollute output too much
    IWRITE=IWRITE+1
  ENDIF  
ENDIF

IF (LHOOK) CALL DR_HOOK('SATOBFREQ_BYNAM',1,ZHOOK_HANDLE)
RETURN

!---------- Error handling ------------------
902  CONTINUE
CALL ABOR1('SATOBFREQ_BYNAM : NAMELIST - READING ERROR')

903  CONTINUE
CALL ABOR1('SATOBFREQ_BYNAM : NAMELIST - END OF FILE WITHOUT NAMSATFREQ')

END SUBROUTINE SATOBFREQ_BYNAM
