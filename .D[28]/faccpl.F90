SUBROUTINE FACCPL_MT64                                              &
&                     (FA,  KREP,   KRANG,  CDPREF, KNIVAU, CDSUFF, &
&                      PCHAMP, LDCOSP, KVALCO, KLONGD)
USE FA_MOD, ONLY : FA_COM, JPNIIL, FAFICH, FACADR
USE PARKIND1, ONLY : JPRB
USE YOMHOOK , ONLY : LHOOK, DR_HOOK
USE LFI_PRECISION
USE GRIB_API_INTERFACE
USE GRIB_API
IMPLICIT NONE
TYPE(FA_COM) :: FA
INTEGER (KIND=JPLIKB) KREP, KRANG, KNIVAU, KLONGD, ILONGD
!
INTEGER (KIND=JPLIKB) KVALCO(*)
REAL (KIND=JPDBLR) PCHAMP(*)
!
LOGICAL LDCOSP
!
CHARACTER CDPREF*(*), CDSUFF*(*)
!
#include "fagribex.h"
!
REAL (KIND=JPDBLR), ALLOCATABLE :: ZCHAMP (:)
INTEGER (KIND=JPLIKB) IRANGC
INTEGER (KIND=JPLIKB) INLATI, INXLON, IDLUXG, IDGUXG, IDZONL, IDZONG
INTEGER (KIND=JPLIKB) ILCHAM
INTEGER (KIND=JPLIKB) ICPLSIZE
INTEGER (KIND=JPLIKB) ILAT, ILON
INTEGER (KIND=JPLIKB) INIMES, INUMER
INTEGER (KIND=JPLIKB) ILATMIN, ILATMAX, ILONMIN, ILONMAX
!
CHARACTER(LEN=FA%JPLMES) CLMESS
CHARACTER(LEN=FA%JPLSPX) CLNSPR
LOGICAL :: LLFATA
!

TYPE (FACADR), POINTER :: YLCADR
TYPE (FAFICH), POINTER :: YLFICH
CHARACTER, ALLOCATABLE :: CLGRIB (:)
INTEGER (KIND=JPKSIZE_T) :: ILGRIB
INTEGER (KIND=JPLIKM) :: IRET, IGRIBH
INTEGER (KIND=JPLIKB) :: IFGRIB, INBITS, IBFPDG
REAL (KIND=JPDBLR)    :: ZUNDF, ZMAX, ZMIN
LOGICAL               :: LLUNDF
!
REAL(KIND=JPRB) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('FACCPL_MT',0,ZHOOK_HANDLE)

YLFICH => FA%FICHIER(KRANG)
IRANGC = YLFICH%NUCADR
YLCADR => FA%CADRE(IRANGC)

! Save encoding options

IBFPDG = YLFICH%NBFPDG 
IFGRIB = YLFICH%NFGRIB

! Hollow field encoding options

YLFICH%NFGRIB = 140
YLFICH%NBFPDG = YLFICH%NCPLBITS

IF (LDCOSP) THEN
  KREP=-200
  GOTO 1001
ENDIF

KREP=0

INLATI=YLCADR%NLATIT
INXLON=YLCADR%NXLOPA

ILCHAM = INLATI * INXLON
IDLUXG  = YLCADR%NLOPAR (4)  ! lon
IDGUXG  = YLCADR%NLOPAR (6)  ! lat
IDZONL  = YLCADR%NLOPAR (7)  
IDZONG  = YLCADR%NLOPAR (8)

ICPLSIZE = YLFICH%NCPLSIZE

ALLOCATE (ZCHAMP (ILCHAM))

ILONMIN=IDZONL+ICPLSIZE
ILONMAX=IDLUXG-ICPLSIZE-IDZONL+1
ILATMIN=IDZONG+ICPLSIZE
ILATMAX=IDGUXG-ICPLSIZE-IDZONG+1

ZMIN = MINVAL (PCHAMP (1:ILCHAM))
ZMAX = MAXVAL (PCHAMP (1:ILCHAM))

IF (ZMAX > 0) THEN
  ZUNDF = 2.0_JPDBLR * ZMAX
ELSEIF (ZMAX < 0) THEN
  ZUNDF = 0.5_JPDBLR * ZMAX
ELSEIF (ZMIN < 0) THEN
  ZUNDF = 2.0_JPDBLR * ZMIN
ELSEIF (ZMIN > 0) THEN
  ZUNDF = 0.5_JPDBLR * ZMIN
ELSE ! ZMAX=ZMIN=0.
  ZUNDF = 1.0_JPDBLR
ENDIF

DO ILAT = 1, INLATI
  DO ILON = 1, INXLON
    IF ((ILON <= ILONMIN) .OR. (ILON >= ILONMAX) .OR. &
      & (ILAT <= ILATMIN) .OR. (ILAT >= ILATMAX)) THEN
      ZCHAMP ((ILAT-1)*INXLON+ILON) = PCHAMP ((ILAT-1)*INXLON+ILON)
    ELSE
      ZCHAMP ((ILAT-1)*INXLON+ILON) = ZUNDF
    ENDIF
  ENDDO
ENDDO

LLUNDF = .TRUE.
CALL FACGRM_MT64 (FA, KREP, KRANG, CDPREF, KNIVAU, CDSUFF, ZCHAMP, &
                & LDCOSP, IGRIBH, LLUNDF, ZUNDF, 2_JPLIKB)

IF (KREP /= 0) GOTO 1001

CALL IGRIB_SET_VALUE (IGRIBH, 'ICPLSIZE', ICPLSIZE)

CALL IGRIB_GET_VALUE (IGRIBH, 'INBITS', INBITS)

CALL IGRIB_GET_MESSAGE_SIZE (IGRIBH, ILGRIB)

ALLOCATE (CLGRIB (ILGRIB))
CALL GRIB_COPY_MESSAGE (IGRIBH, CLGRIB, STATUS=IRET)

IF (IRET == GRIB_SUCCESS) THEN
  ILONGD = 4+(ILGRIB+7)/8
  KVALCO (1) = IFGRIB
  KVALCO (2) = 0
  KVALCO (3) = ICPLSIZE
  KVALCO (4) = INBITS
  IF ((KLONGD < ILONGD) .AND. (KLONGD > 0)) THEN
    KREP=-130
    GOTO 1001
  ELSE
    KLONGD = ILONGD
  ENDIF
  KVALCO (5:ILONGD) = TRANSFER (CLGRIB, KVALCO (5:ILONGD))
ELSE
  KREP = IRET-1000
  GOTO 1001
ENDIF

CALL IGRIB_RELEASE (IGRIBH)

1001 CONTINUE

IF (ALLOCATED (CLGRIB)) DEALLOCATE (CLGRIB)
IF (ALLOCATED (ZCHAMP)) DEALLOCATE (ZCHAMP)

! Restore encoding options

YLFICH%NBFPDG = IBFPDG 
YLFICH%NFGRIB = IFGRIB 

!

LLFATA=LLMOER (KREP,KRANG)

IF (FA%LFAMOP.OR.LLFATA) THEN
  INIMES=2
  CLNSPR='FACCPL'
  INUMER=JPNIIL

  WRITE (UNIT=CLMESS,FMT="('KREP=',I4,', KRANG=',I4,         &
&   ', CDPREF=''',A,''', KNIVAU=',I4,', CDSUFF=''',A,'''')") &
&             KREP, KRANG, CDPREF, KNIVAU, CDSUFF
  CALL FAIPAR_MT64                                       &
&                 (FA,INUMER,INIMES,KREP,.FALSE.,CLMESS, &
&                  CLNSPR, '',.FALSE.)
ENDIF

IF (LHOOK) CALL DR_HOOK('FACCPL_MT',1,ZHOOK_HANDLE)

CONTAINS

#include "facom2.llmoer.h"

END SUBROUTINE 

