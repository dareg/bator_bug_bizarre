!****-------------------------------------------------------------------
!****   QASTAT: Fonctions in-line modelisant les correlations entre les
!****   ------  erreurs de l'ebauche
!****-------------------------------------------------------------------
!***  HYPOTHESES DE BASE: MODELE SEPARABLE
!***  ------------------
!***  ON SUPPOSE L'EXISTENCE D'UN MODELE FH DE CORRELATION HORIZONTALE
!*** (ASSORTI DE "PORTEES" DIFFERENTES SELON LES CHAMPS)
!***  POUR LE MOMENT FH EST UNE GAUSSIENNE;
!***                      ET D'UN MODELE FV DE CORRELATION VERTICALE
!***  (ASSORTI DE COEFFICIENTS DIFFERENTS SELON LES CHAMPS)
!***
!***  METHODE: ON EN DEDUIT (PAR DERIVATION DES COVARIANCES) LES CORRELATIONS
!***  -------  CROISEES ENTRE ERREURS DE GEOPOTENTIEL, VENTS, TEMPERATURE ET
!***           EPAISSEURS.
!***
!***  ATTENTION : SI L'ON CHANGE L'UN DES MODELES FH OU FV, IL FAUT
!***              IMPERATIVEMENT REECRIRE LES FONCTIONS DERIVEES
!****-------------------------------------------------------------------
! 0. DEFINITION DES VARIABLES UTILISEES
!    ==================================
! 0.1 VARIABLES DES FONCTIONS IN-LINE:
!     -------------------------------
!   - PD2  : CARRE DE LA DISTANCE ENTRE LES 2 POINTS CONCERNES (en m**2);
!   - P.   : PRESSION (PASCALS) DES POINTS CONCERNES;

!   - PC1,PS1: COSINUS ET SINUS DE L'ANGLE ENTRE L'EST AU POINT 1 ET L'ARC DE
!                              CERCLE ORIENTE DU POINT 1 AU POINT 2;
!   - PC2,PS2: COSINUS ET SINUS DE L'ANGLE ENTRE L'EST AU POINT 2 ET L'ARC DE
!                              CERCLE ORIENTE DU POINT 1 AU POINT 2;
!            (CES COSINUS ET SINUS SONT CALCULES DANS LE CD QADITS)

!   - PET..: ECART-TYPE D'ERREUR DU GUESS POUR PARAMETRE ET POINT CONCERNES;

!   - POR    : CARRE DE LA PORTEE HORIZONTALE POUR UN CHAMP
!   - POZ    : CARRE DE LA PORTEE HORIZONTALE POUR LE GEOPOTENTIEL
!   - POD    : CARRE DE LA PORTEE HORIZONTALE POUR LE VENT DIVERGENT
!   - POZ    : CARRE DE LA PORTEE HORIZONTALE POUR L'HUMIDITE
!   - POST   : CARRE DE LA PORTEE HORIZONTALE POUR LE CHAMP DE SST

!   - POSN   : CARRE DE LA PORTEE "VERTICALE" POUR LE CHAMP DE NEIGE SN

!   - PK     : COEFFICIENT K DE LA CORRELATION VERTICALE POUR UN CHAMP
!   - PKZ    : COEFFICIENT K DE LA CORRELATION VERTICALE DU GEOPOTENTIEL
!   - PKH    : COEFFICIENT K DE LA CORRELATION VERTICALE DE L'HUMIDITE

!   - PNU2   : CARRE DU RAPPORT NU ENTRE EC.TYPES DE VENT DIVERGENT ET ROTAT.
!   - PMU    : COEFFICIENT DE CORRELATION ENTRE PHI ET PSI
!=======================================================================
! 1. MODELES DE CORRELATION
!    ======================
! 1.1 MODELES DE BASE:
!     ---------------
! ex-LOG DIVERS:
REAL(KIND=JPRB) :: FDLP,FDLP2
REAL(KIND=JPRB) :: P1,P2
FDLP (P1,P2) = (P2-P1)
FDLP2(P1,P2) = (P2-P1) * (P2-P1)
! SUR L'HORIZONTALE:
REAL(KIND=JPRB) :: FH, FHMESAN
REAL(KIND=JPRB) :: PD2,POR
FH(PD2,POR) = EXP ( -0.5_JPRB*PD2 / POR )
FHMESAN(PD2,POR) = 0.5_JPRB*( EXP(-PD2/POR)+ &
     & (1.0_JPRB+2.0_JPRB*PD2/POR)*EXP(-2.0_JPRB*PD2/POR) )
! SUR LA "VERTICALE" pour le champ de neige SN:
REAL(KIND=JPRB) :: FHP
REAL(KIND=JPRB) :: PP2,POSN
FHP(PP2,POSN) = EXP ( -0.5_JPRB*PP2 / POSN )
! SUR LA VERTICALE:
REAL(KIND=JPRB) :: FV
REAL(KIND=JPRB) :: PK
FV(P1,P2,PK) = 1.0_JPRB / (1.0_JPRB + PK * FDLP2(P1,P2))

! 1.2. MODELES DERIVES SUR L'HORIZONTALE:  ( TERME GENERIQUE : FRH_X_Y )
!      ---------------------------------
! GEOPOTENTIEL:
REAL(KIND=JPRB) :: FRHZZ
REAL(KIND=JPRB) :: POZ
FRHZZ(PD2,POZ) = FH(PD2,POZ)
! TEMPERATURE:
REAL(KIND=JPRB) :: FRHTT,FRHT2T2,FRHTZ,FRHZT, FRHT2T2_MESAN 
FRHTT(PD2,POZ) = FH(PD2,POZ)
FRHT2T2(PD2,POZ) = FRHTT(PD2,POZ)
FRHT2T2_MESAN(PD2,POZ)=FHMESAN(PD2,POZ)
FRHTZ(PD2,POZ) = FH(PD2,POZ)
FRHZT(PD2,POZ) = FH(PD2,POZ)
! HUMIDITE:
REAL(KIND=JPRB) :: FRHHH,FRHH2H2, FRHH2H2_MESAN 
FRHHH(PD2,POZ) = FH(PD2,POZ)
FRHH2H2(PD2,POZ) =  FRHHH(PD2,POZ)
FRHH2H2_MESAN(PD2,POZ)=FHMESAN(PD2,POZ)
! NEIGE SN:
REAL(KIND=JPRB) :: FRHSNSN,FRHPSNSN
FRHSNSN(PD2,POZ) = FH(PD2,POZ)
FRHPSNSN(PP2,POSN) = FHP(PP2,POSN)
! SST:
REAL(KIND=JPRB) :: FRHSTST
FRHSTST(PD2,POZ) = FH(PD2,POZ)
! VENT U ET V:
REAL(KIND=JPRB) :: FRHUU,FRHUV,FRHVU,FRHVV,FRHUZ,FRHZU,FRHVZ,FRHZV
REAL(KIND=JPRB) :: FRHUT,FRHTU,FRHVT,FRHTV,FRHU1U1,FRHU1V1,FRHV1U1,FRHV1V1  
REAL(KIND=JPRB) :: POD,PNU2,PC1,PC2,PS1,PS2,PMU
FRHUU(PD2,POZ,POD,PNU2,PC1,PC2,PS1,PS2) =&
         &( (PC1*PC2 + (1.0_JPRB-PD2/POZ)*PS1*PS2) * FH(PD2,POZ) +&
    &PNU2 * (PC1*PC2*(1.0_JPRB-PD2/POD) + PS1*PS2) * FH(PD2,POD) )&
         &/ (1.0_JPRB+PNU2)
FRHU1U1(PD2,POZ,POD,PNU2,PC1,PC2,PS1,PS2) =&
    &FRHUU(PD2,POZ,POD,PNU2,PC1,PC2,PS1,PS2)
FRHUV(PD2,POZ,POD,PNU2,PC1,PC2,PS1,PS2) =&
         &( (PC1*PS2 - (1.0_JPRB-PD2/POZ)*PS1*PC2) * FH(PD2,POZ) +&
    &PNU2 * (PC1*PS2*(1.0_JPRB-PD2/POD) - PS1*PC2) * FH(PD2,POD) )&
         &/ (1.0_JPRB+PNU2)
FRHU1V1(PD2,POZ,POD,PNU2,PC1,PC2,PS1,PS2) =&
    &FRHUV(PD2,POZ,POD,PNU2,PC1,PC2,PS1,PS2)
FRHVU(PD2,POZ,POD,PNU2,PC1,PC2,PS1,PS2) =&
         &( (PS1*PC2 - (1.0_JPRB-PD2/POZ)*PC1*PS2) * FH(PD2,POZ) +&
    &PNU2 * (PS1*PC2*(1.0_JPRB-PD2/POD) - PC1*PS2) * FH(PD2,POD) )&
         &/ (1.0_JPRB+PNU2)
FRHV1U1(PD2,POZ,POD,PNU2,PC1,PC2,PS1,PS2) =&
    &FRHVU(PD2,POZ,POD,PNU2,PC1,PC2,PS1,PS2)
FRHVV(PD2,POZ,POD,PNU2,PC1,PC2,PS1,PS2) =&
         &( (PS1*PS2 + (1.0_JPRB-PD2/POZ)*PC1*PC2) * FH(PD2,POZ) +&
    &PNU2 * (PS1*PS2*(1.0_JPRB-PD2/POD) + PC1*PC2) * FH(PD2,POD) )&
         &/ (1.0_JPRB+PNU2)
FRHV1V1(PD2,POZ,POD,PNU2,PC1,PC2,PS1,PS2) =&
    &FRHVV(PD2,POZ,POD,PNU2,PC1,PC2,PS1,PS2)
FRHUZ(PD2,POZ,PNU2,PMU,PS1) = -&
             &PMU * PS1 * FH(PD2,POZ) * SQRT(PD2/(POZ*(1.0_JPRB+PNU2)))
FRHZU(PD2,POZ,PNU2,PMU,PS2) =&
             &PMU * PS2 * FH(PD2,POZ) * SQRT(PD2/(POZ*(1.0_JPRB+PNU2)))
FRHVZ(PD2,POZ,PNU2,PMU,PC1) =&
             &PMU * PC1 * FH(PD2,POZ) * SQRT(PD2/(POZ*(1.0_JPRB+PNU2)))
FRHZV(PD2,POZ,PNU2,PMU,PC2) = -&
             &PMU * PC2 * FH(PD2,POZ) * SQRT(PD2/(POZ*(1.0_JPRB+PNU2)))

FRHUT(PD2,POZ,PNU2,PMU,PS1) = FRHUZ(PD2,POZ,PNU2,PMU,PS1)
FRHTU(PD2,POZ,PNU2,PMU,PS2) = FRHZU(PD2,POZ,PNU2,PMU,PS2)
FRHVT(PD2,POZ,PNU2,PMU,PC1) = FRHVZ(PD2,POZ,PNU2,PMU,PC1)
FRHTV(PD2,POZ,PNU2,PMU,PC2) = FRHZV(PD2,POZ,PNU2,PMU,PC2)

! 1.3. MODELES DERIVES POUR LA VERTICALE:  ( TERME GENERIQUE : FRV_X_Y )
!      ---------------------------------
! GEOPOTENTIEL:
REAL(KIND=JPRB) :: FRVZZ
REAL(KIND=JPRB) :: PKZ
FRVZZ(P1,P2,PKZ) = FV(P1,P2,PKZ)
! TEMPERATURE:
REAL(KIND=JPRB) :: FRVTT,FRVZT,FRVTZ
FRVTT(P1,P2,PKZ) = (1.0_JPRB-3._JPRB*PKZ*FDLP2(P1,P2)) * FV(P1,P2,PKZ)**3
FRVZT(P1,P2,PKZ) = SQRT(2.0_JPRB*PKZ) * FDLP(P1,P2) * FV(P1,P2,PKZ)**2
FRVTZ(P1,P2,PKZ) = - FRVZT(P1,P2,PKZ)
!  HUMIDITE:
REAL(KIND=JPRB) :: FRVHH
REAL(KIND=JPRB) :: PKH
FRVHH(P1,P2,PKH) = FV(P1,P2,PKH)
! VENTS U ET V:
REAL(KIND=JPRB) :: FRVUU,FRVUV,FRVUZ,FRVUT,FRVVU,FRVVV,FRVVZ,FRVVT
REAL(KIND=JPRB) :: FRVZU,FRVZV,FRVTU,FRVTV
FRVUU(P1,P2,PKZ) = FRVZZ(P1,P2,PKZ)
FRVUV(P1,P2,PKZ) = FRVZZ(P1,P2,PKZ)
FRVUZ(P1,P2,PKZ) = FRVZZ(P1,P2,PKZ)
FRVUT(P1,P2,PKZ) = FRVZT(P1,P2,PKZ)
FRVVU(P1,P2,PKZ) = FRVZZ(P1,P2,PKZ)
FRVVV(P1,P2,PKZ) = FRVZZ(P1,P2,PKZ)
FRVVZ(P1,P2,PKZ) = FRVZZ(P1,P2,PKZ)
FRVVT(P1,P2,PKZ) = FRVZT(P1,P2,PKZ)
FRVZU(P1,P2,PKZ) = FRVZZ(P1,P2,PKZ)
FRVZV(P1,P2,PKZ) = FRVZZ(P1,P2,PKZ)
FRVTU(P1,P2,PKZ) = FRVTZ(P1,P2,PKZ)
FRVTV(P1,P2,PKZ) = FRVTZ(P1,P2,PKZ)
! PBL PARAMETERS (T2m,H2m,V10m)
REAL(KIND=JPRB) :: FRVBLBL
FRVBLBL(P1,P2,PKH) = FV(P1,P2,PKH)
!=============================================================================
! 2. CAS PARTICULIER DES EPAISSEURS
!    ==============================
REAL(KIND=JPRB) :: FSIGDZ,FRVDZDZ,FRVDZZ,FRVDZT,FRVDZU,FRVDZV,FRVZDZ
REAL(KIND=JPRB) :: FRVTDZ,FRVUDZ,FRVVDZ
REAL(KIND=JPRB) :: PETZS,PETZI,PS,PI,P1S,P1I,P2S,P2I,PETZ1S,PETZ1I,PETZ2S,PETZ2I
FSIGDZ(PETZS,PETZI,PS,PI,PKZ) = SQRT (&
    &PETZS**2 + PETZI**2 - 2.0_JPRB * PETZS * PETZI * FRVZZ(PS,PI,PKZ) )

FRVDZDZ(P1S,P1I,P2S,P2I,PETZ1S,PETZ1I,PETZ2S,PETZ2I,PKZ) =&
      &(  PETZ1S * PETZ2S * FRVZZ(P1S,P2S,PKZ)&
       &+ PETZ1I * PETZ2I * FRVZZ(P1I,P2I,PKZ)&
       &- PETZ1S * PETZ2I * FRVZZ(P1S,P2I,PKZ)&
       &- PETZ1I * PETZ2S * FRVZZ(P1I,P2S,PKZ)  )&
      &/ FSIGDZ(PETZ1S,PETZ1I,P1S,P1I,PKZ)&
      &/ FSIGDZ(PETZ2S,PETZ2I,P2S,P2I,PKZ)

FRVDZZ(P1S,P1I,P2,PETZ1S,PETZ1I,PKZ) =&
     &(  PETZ1S * FRVZZ(P1S,P2,PKZ)&
      &- PETZ1I * FRVZZ(P1I,P2,PKZ)  )&
     &/ FSIGDZ(PETZ1S,PETZ1I,P1S,P1I,PKZ)

FRVDZT(P1S,P1I,P2,PETZ1S,PETZ1I,PKZ) =&
     &(  PETZ1S * FRVZT(P1S,P2,PKZ)&
      &- PETZ1I * FRVZT(P1I,P2,PKZ)  )&
     &/ FSIGDZ(PETZ1S,PETZ1I,P1S,P1I,PKZ)

FRVDZU(P1S,P1I,P2,PETZ1S,PETZ1I,PKZ) =&
     &(  PETZ1S * FRVZU(P1S,P2,PKZ)&
      &- PETZ1I * FRVZU(P1I,P2,PKZ)  )&
     &/ FSIGDZ(PETZ1S,PETZ1I,P1S,P1I,PKZ)

FRVDZV(P1S,P1I,P2,PETZ1S,PETZ1I,PKZ) =&
     &(  PETZ1S * FRVZV(P1S,P2,PKZ)&
      &- PETZ1I * FRVZV(P1I,P2,PKZ)  )&
     &/ FSIGDZ(PETZ1S,PETZ1I,P1S,P1I,PKZ)

FRVZDZ(P1,P2S,P2I,PETZ2S,PETZ2I,PKZ) =&
     &(  PETZ2S * FRVZZ(P1,P2S,PKZ)&
      &- PETZ2I * FRVZZ(P1,P2I,PKZ)  )&
     &/ FSIGDZ(PETZ2S,PETZ2I,P2S,P2I,PKZ)

FRVTDZ(P1,P2S,P2I,PETZ2S,PETZ2I,PKZ) =&
     &(  PETZ2S * FRVTZ(P1,P2S,PKZ)&
      &- PETZ2I * FRVTZ(P1,P2I,PKZ)  )&
     &/ FSIGDZ(PETZ2S,PETZ2I,P2S,P2I,PKZ)

FRVUDZ(P1,P2S,P2I,PETZ2S,PETZ2I,PKZ) =&
     &(  PETZ2S * FRVUZ(P1,P2S,PKZ)&
      &- PETZ2I * FRVUZ(P1,P2I,PKZ)  )&
     &/ FSIGDZ(PETZ2S,PETZ2I,P2S,P2I,PKZ)

FRVVDZ(P1,P2S,P2I,PETZ2S,PETZ2I,PKZ) =&
     &(  PETZ2S * FRVVZ(P1,P2S,PKZ)&
      &- PETZ2I * FRVVZ(P1,P2I,PKZ)  )&
     &/ FSIGDZ(PETZ2S,PETZ2I,P2S,P2I,PKZ)
!                                                      *****************
!                                                      * FIN DE QASTAT *
!****--------------------------------------------------*****************
