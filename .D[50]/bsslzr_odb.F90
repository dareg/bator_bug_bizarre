SUBROUTINE BSSLZR_ODB(PBES,KNUM)

!**** *BSSLZR_ODB* - ROUTINE TO RETURN ZEROS OF THE J0 *BESSEL FUNCTION.

!     PURPOSE.
!     --------

!          *BSSLZR_ODB* RETURNS *KNUM* ZEROS, OR IF *KNUM>50,* *KNUM*
!     APPROXIMATE ZEROS OF THE *BESSEL FUNCTION J0.

!**   INTERFACE.
!     ----------

!          *CALL* *BSSLZR_ODB(PBES,KNUM)*

!               *PBES*   - ARRAY, DIMENSIONED *KNUM,* TO RECEIVE THE
!                          VALUES.
!               *KNUM*   - NUMBER OF ZEROS REQUESTED.

!     METHOD.
!     -------

!          THE FIRST 50 VALUES ARE OBTAINED FROM A LOOK UP TABLE. ANY
!     ADDITIONAL VALUES REQUESTED ARE INTERPOLATED.

!     EXTERNALS.
!     ----------

!          NONE.

!     REFERENCE.
!     ----------


USE PARKIND1  ,ONLY : JPIM     ,JPRD

IMPLICIT NONE


INTEGER(KIND=JPIM), intent(in) :: KNUM
REAL(KIND=JPRD), intent(out)   :: PBES(KNUM)
! === END OF INTERFACE BLOCK ===

!     LOCAL INTEGER SCALARS
INTEGER(KIND=JPIM) :: INUM, J

DIMENSION ZBES(50)

!     LOCAL REAL SCALARS
REAL(KIND=JPRD) :: ZAPI, ZBES, ZPI

DATA ZBES        / 2.4048255577_JPRD,   5.5200781103_JPRD,&
    &8.6537279129_JPRD,  11.7915344391_JPRD,  14.9309177086_JPRD,  18.0710639679_JPRD,&
   &21.2116366299_JPRD,  24.3524715308_JPRD,  27.4934791320_JPRD,  30.6346064684_JPRD,&
   &33.7758202136_JPRD,  36.9170983537_JPRD,  40.0584257646_JPRD,  43.1997917132_JPRD,&
   &46.3411883717_JPRD,  49.4826098974_JPRD,  52.6240518411_JPRD,  55.7655107550_JPRD,&
   &58.9069839261_JPRD,  62.0484691902_JPRD,  65.1899648002_JPRD,  68.3314693299_JPRD,&
   &71.4729816036_JPRD,  74.6145006437_JPRD,  77.7560256304_JPRD,  80.8975558711_JPRD,&
   &84.0390907769_JPRD,  87.1806298436_JPRD,  90.3221726372_JPRD,  93.4637187819_JPRD,&
   &96.6052679510_JPRD,  99.7468198587_JPRD, 102.8883742542_JPRD, 106.0299309165_JPRD,&
  &109.1714896498_JPRD, 112.3130502805_JPRD, 115.4546126537_JPRD, 118.5961766309_JPRD,&
  &121.7377420880_JPRD, 124.8793089132_JPRD, 128.0208770059_JPRD, 131.1624462752_JPRD,&
  &134.3040166383_JPRD, 137.4455880203_JPRD, 140.5871603528_JPRD, 143.7287335737_JPRD,&
  &146.8703076258_JPRD, 150.0118824570_JPRD, 153.1534580192_JPRD, 156.2950342685_JPRD/


!     ------------------------------------------------------------------

!*         1.     EXTRACT VALUES FROM LOOK UP TABLE.


100 CONTINUE
ZAPI=2.0_JPRD*ASIN(1.0_JPRD)
INUM=MIN(KNUM,50)

DO 110 J=1,INUM
  PBES(J)=ZBES(J)
110 CONTINUE

!     ------------------------------------------------------------------

!*         2.     INTERPOLATE REMAINING VALUES.
!                 ----------- --------- -------

200 CONTINUE

IF (KNUM > 50) THEN
  ZPI=ZAPI
  DO 210 J=51,KNUM
    PBES(J)=PBES(J-1)+ZPI
  210 CONTINUE
ENDIF

!     ------------------------------------------------------------------

RETURN
END SUBROUTINE BSSLZR_ODB
