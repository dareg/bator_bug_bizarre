SUBROUTINE EGGMLT (PRPI, KDLUX, KDLUN, KDGUX, KDGUN, KULOUT,&
 & KPRINT, PRPK, PLON0U, PLON1U, PLON2U, KSOTRP, PLAT1R, PLAT2R,&
 & PHSUD,PBETA)  

!--------------------------------------------------------------------------
!     GEOGRAPHY OF GRID-POINTS AUXILLARY ROUTINE
!     LATITUDE FOR ISOTROPIC GRID
!     ARPEGE-ALADIN
!     ------------------------------------------

!     GIVEN THE PROJECTION PARAMETER (PRPK), THE LONGITUDES AND
!     ONE LATITUDE, EGGMLT PROVIDES THE OTHER LATITUDE IN SUCH A WAY
!     THAT THE GRID SIZE WILL BE THE SAME FOLLOWING X AND Y

!     THIS ROUTINE IS CALLED AND CAN BE USED ONLY IN CONNECTION WITH
!     SUBROUTINE EGGX

!     INPUT PARAMETERS
!     ----------------
!      PRPI :  PI
!      KDLUN, KDLUX : ADRESSES OF EXTREME POINTS OF USEFUL DOMAIN IN X
!      KDGUN, KDGUX :                                                Y
!      KULOUT : OUTPUT FILE UNIT
!      KPRINT : IMPRESSIONS SEULEMENT SI KPRINT = 1
!      PRPK : PROJECTION PARAMETER, SEE EGGX
!      PLON0U : REFERENCE LONGITUDE (MODIFIED FOR DOMAINS ASTRIDE GREEWICH)
!      PLON1U : LONGITUDE OF SW CORNER (EQUALLY MODIFIED)
!      PLON2U : LONGITUDE OF NE CORNER
!      KSOTRP = 0, PLAT1R, PLAT2R KNOWN : EGGMLT SHOULD NOT BE CALLED
!             = 1, PLAT1R KNOWN, EGGMLT COMPUTES PLAT2R
!             = 2, PLAT2R KONWN, EGGMLT COMPUTES PLAT1R
!      PLAT1R : LATITUDE OF SW CORNER
!      PLAT2R : LATITUDE OF NE CORNER
!      PHSUD  = 1., NORTH HEMISPHERE MAPPING OR MERCATOR
!             = -1., SOUTH HEMISPHERE MAPPING
!      PBETA  : ANGLE BETWEEN X-AXIS AND LATITUDE AT PLON0

!     OUTPUT PARAMETERS
!     -----------------
!      EITHER PLAT1R IF KSOTRP = 2
!      OR     PLAT2R IF KSOTRP = 1

!     WRITTEN BY
!     ---------- ALAIN JOLY

!     ORIGINAL NORTHERN HEMISPHERE VERSION : 31/1/92
!     SOUTH HEMISPEHER VERSION : 27/1/93
!--------------------------------------------------------------------------

USE PARKIND1  ,ONLY : JPIM     ,JPRB
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK

!--------------------------------------------------------------------------

IMPLICIT NONE

REAL(KIND=JPRB)   ,INTENT(IN)    :: PRPI 
INTEGER(KIND=JPIM),INTENT(IN)    :: KDLUX 
INTEGER(KIND=JPIM),INTENT(IN)    :: KDLUN 
INTEGER(KIND=JPIM),INTENT(IN)    :: KDGUX 
INTEGER(KIND=JPIM),INTENT(IN)    :: KDGUN 
INTEGER(KIND=JPIM),INTENT(IN)    :: KULOUT 
INTEGER(KIND=JPIM),INTENT(IN)    :: KPRINT 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PRPK 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLON0U 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLON1U 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PLON2U 
INTEGER(KIND=JPIM),INTENT(IN)    :: KSOTRP 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PLAT1R 
REAL(KIND=JPRB)   ,INTENT(INOUT) :: PLAT2R 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PHSUD 
REAL(KIND=JPRB)   ,INTENT(IN)    :: PBETA 

!--------------------------------------------------------------------------

REAL(KIND=JPRB) :: ZCS, ZCS1, ZCS2, ZDCLA1, ZDCLA2, ZPIS2, ZPIS4,&
 & ZRAPP, ZTG1, ZTG2, ZTGK1, ZTGK2  
REAL(KIND=JPRB) :: ZHOOK_HANDLE

!--------------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('EGGMLT',0,ZHOOK_HANDLE)
!--------------------------------------------------------------------------

IF ( KPRINT == 1 ) THEN
  WRITE (KULOUT,*) ' '
  WRITE (KULOUT,*) ' --- EGGMLT --- '
  WRITE (KULOUT,*) ' '
ENDIF
ZPIS2 = PRPI*0.5_JPRB
ZPIS4 = PRPI*0.25_JPRB

IF ( KSOTRP >= 1.AND. PRPK /= 0.0_JPRB ) THEN

  ! * STEREO LAMBERT PROJECTION
  ZRAPP = PHSUD*(REAL( KDLUX-KDLUN ,JPRB)/REAL( KDGUX-KDGUN ,JPRB))
  ZCS1 = COS( PRPK*(PLON1U-PLON0U)-PBETA )*ZRAPP +&
   & SIN( PRPK*(PLON1U-PLON0U)-PBETA )  
  IF ( KPRINT == 1 ) THEN
    WRITE (KULOUT,*) ' ZRAPP = ',ZRAPP
    WRITE (KULOUT,*) ' PLON1U = ',PLON1U,' 0U = ',PLON0U
    WRITE (KULOUT,*) ' PLON1U-PLON0U ',PLON1U-PLON0U
  ENDIF
  ZCS2 = COS( PRPK*(PLON2U-PLON0U)-PBETA )*ZRAPP +&
   & SIN( PRPK*(PLON2U-PLON0U)-PBETA )  
  IF ( KSOTRP == 1 ) THEN
    ZDCLA1 = ZPIS4 - 0.5_JPRB*PLAT1R
    ZTGK1 = TAN( ZDCLA1 )**PRPK
    ZTG2 = ( ZTGK1*ZCS1/ZCS2 )**(1.0_JPRB/PRPK)
    PLAT2R = ZPIS2 - 2.0_JPRB*ATAN( ZTG2 )
    IF ( KPRINT == 1 ) THEN
      WRITE (KULOUT,*) ' '
      WRITE (KULOUT,*) ' NE LATITUDE PLAT2 MODIFIED '
      WRITE (KULOUT,*) ' ZTGK1 = ',ZTGK1
      WRITE (KULOUT,*) ' ZCS1 = ',ZCS1
      WRITE (KULOUT,*) ' ZCS2 = ',ZCS2
      WRITE (KULOUT,*) ' ROTATED LATITUDE LAT2 R = ',PLAT2R
    ENDIF
  ENDIF
  IF ( KSOTRP == 2 ) THEN
    ZDCLA2 = ZPIS4 - 0.5_JPRB*PLAT2R
    ZTGK2 = TAN( ZDCLA2 )**PRPK
    ZTG1 = ( ZTGK2*ZCS2/ZCS1 )**(1.0_JPRB/PRPK)
    PLAT1R = ZPIS2 - 2.0_JPRB*ATAN( ZTG1 )
    IF ( KPRINT == 1 ) THEN
      WRITE (KULOUT,*) ' '
      WRITE (KULOUT,*) ' NE LATITUDE PLAT1 MODIFIED '
      WRITE (KULOUT,*) ' ROTATED LATITUDE LAT1 R = ',PLAT1R
    ENDIF
  ENDIF
ELSEIF ( KSOTRP >= 1.AND. PRPK == 0.0_JPRB ) THEN

  ! * MERCATOR PROJECTION
  ZRAPP = REAL( KDGUX-KDGUN ,JPRB)/REAL( KDLUX-KDLUN ,JPRB)
  ZCS = ( PLON2U - PLON1U )*(ZRAPP*COS(PBETA)+SIN(PBETA))&
   & /(COS(PBETA)-ZRAPP*SIN(PBETA))  
  IF ( KSOTRP == 1 ) THEN
    ZDCLA1 = ZPIS4 - 0.5_JPRB*PLAT1R
    ZTGK1 = LOG( TAN( ZDCLA1 ) )
    ZTG2 = EXP( ZTGK1 - ZCS )
    PLAT2R = ZPIS2 - 2.0_JPRB*ATAN( ZTG2 )
    IF ( KPRINT == 1 ) THEN
      WRITE (KULOUT,*) ' '
      WRITE (KULOUT,*) ' NE LATITUDE PLAT2 MODIFIED '
      WRITE (KULOUT,*) ' ROTATED LATITUDE LAT2 R = ',PLAT2R
    ENDIF
  ENDIF
  IF ( KSOTRP == 2 ) THEN
    ZDCLA2 = ZPIS4 - 0.5_JPRB*PLAT2R
    ZTGK2 = LOG( TAN( ZDCLA2 ) )
    ZTG1 = EXP( ZTGK2 + ZCS )
    PLAT1R = ZPIS2 - 2.0_JPRB*ATAN( ZTG1 )
    IF ( KPRINT == 1 ) THEN
      WRITE (KULOUT,*) ' '
      WRITE (KULOUT,*) ' NE LATITUDE PLAT1 MODIFIED '
      WRITE (KULOUT,*) ' ROTATED LATITUDE LAT1 R = ',PLAT1R
    ENDIF
  ENDIF
ELSEIF (KSOTRP == 0 ) THEN
  WRITE (KULOUT,*) ' EGGMLT USELESSLY CALLED '
ENDIF

IF ( KPRINT == 1 ) THEN
  WRITE (KULOUT,*) ' EGGMLT OVER '
  WRITE (KULOUT,*) ' '
ENDIF

!--------------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('EGGMLT',1,ZHOOK_HANDLE)
END SUBROUTINE EGGMLT
