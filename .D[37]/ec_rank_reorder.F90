SUBROUTINE EC_RANK_REORDER(KCOMM, KNEWCOMM)
USE PARKIND1 ,ONLY : JPIM
USE MPL_MPIF ,ONLY : MPI_INTEGER4, MPI_COMM_WORLD

IMPLICIT NONE

INTEGER(KIND=JPIM), INTENT(IN)  :: KCOMM
INTEGER(KIND=JPIM), INTENT(OUT) :: KNEWCOMM
INTEGER(KIND=JPIM) :: INEWCOMM, IME, INPES, IRET, ISTAT, IGRP, IGRPWORLD, JROC, INPES_WORLD
CHARACTER(LEN=512) :: CL_RANK_REORDER_FILE
INTEGER(KIND=JPIM), ALLOCATABLE :: IRANK(:)

CALL MPI_COMM_RANK(KCOMM,IME,IRET)

IF (IME == 0) THEN ! Only the master task reads
   CALL EC_GETENV('EC_RANK_REORDER',CL_RANK_REORDER_FILE) ! check if the file is specified via env. variable "EC_RANK_REORDER"
   IF (CL_RANK_REORDER_FILE == ' ') THEN ! no; then check the file "EC_RANK_REORDER" itself in the current directory
      CL_RANK_REORDER_FILE = 'EC_RANK_REORDER'
   ENDIF
   OPEN(111,FILE=trim(CL_RANK_REORDER_FILE),IOSTAT=ISTAT,ACTION='READ',FORM='FORMATTED')
   IF (ISTAT == 0) THEN ! The file exists & can be accessible
      CALL MPI_COMM_SIZE(KCOMM, INPES, IRET)
      ALLOCATE(IRANK(0:INPES-1))
      READ(111,*,IOSTAT=ISTAT) IRANK ! reordered ranks
      CLOSE(111)
      IF (ISTAT == 0) THEN
1000     format(1x,a,:,i0,a,i0,a,i0)
         WRITE(*,1000) 'EC_RANK_REORDER: FILE='//trim(CL_RANK_REORDER_FILE)
         WRITE(*,1000) 'EC_RANK_REORDER: COMM=',KCOMM,' : NPROCS=',INPES
         CALL MPI_COMM_SIZE(MPI_COMM_WORLD, INPES_WORLD, IRET)
         WRITE(*,1000) 'EC_RANK_REORDER: MPI_COMM_WORLD=',MPI_COMM_WORLD,' : NPROCS=',INPES_WORLD
         DO JROC=0,INPES-1
            WRITE(*,1000) 'EC_RANK_REORDER: IRANK(',JROC,') => ',IRANK(JROC)
         ENDDO
      ELSE
         WRITE(0,*) 'EC_RANK_REORDER: Error in reading rank ids from file "'//trim(CL_RANK_REORDER_FILE)//'"'
         INPES = -1
      ENDIF
   ELSE
      INPES = 0
   ENDIF
ENDIF

CALL MPI_BCAST(INPES,1,MPI_INTEGER4,0,KCOMM,IRET)

IF (INPES > 0) THEN
   IF (IME > 0) ALLOCATE(IRANK(0:INPES-1))
   CALL MPI_BCAST(IRANK,INPES,MPI_INTEGER4,0,KCOMM,IRET)
   CALL MPI_COMM_GROUP(KCOMM,IGRPWORLD,IRET)
   CALL MPI_GROUP_INCL(IGRPWORLD,INPES,IRANK,IGRP,IRET)
   CALL MPI_COMM_CREATE(KCOMM,IGRP,KNEWCOMM,IRET)
   IF (IME == 0) THEN
      CALL MPI_COMM_SIZE(KNEWCOMM, INPES, IRET)
      WRITE(*,1000) 'EC_RANK_REORDER: NEWCOMM=',KNEWCOMM,' : NPROCS=',INPES
   ENDIF
ELSE IF (INPES == 0) THEN ! No need for rank reordering detected
   KNEWCOMM = KCOMM
ELSE ! Abort
   CALL MPI_ABORT(KCOMM,INPES,IRET)
ENDIF

IF (ALLOCATED(IRANK)) DEALLOCATE(IRANK)
 
END SUBROUTINE EC_RANK_REORDER
