PROGRAM PERTCMA

!
! PROGRAM TO PERTURB OBSERVATIONS AFTER SCREENING
! AND EVENTUALLY ODB COMPRESSION
!
! Usage:
!       PERTCMA SEED ECMA/CCMA (default CCMA)
!                  SEED is mandatory arg, integer
!
! External: 
!       ECMWF Random generator
!
! Technicals:
!       Every obs is perturbed by Z
!       where Z=r*n, n: random ~ N(0,1); r: obs error
!
! Andrea Storto            *met.no* 2008-05-29
!

USE PARKIND1
USE ODB_MODULE
USE RANDOM_NUMBERS_MIX

IMPLICIT NONE

! ODB admin
INTEGER(KIND=JPIM) :: H, RC, NRA, NROWS,NCOLS
INTEGER(KIND=JPIM) :: NPOOLS, J, JP, NPROC, MYPROC
INTEGER(KIND=JPIM) :: JR, JC
CHARACTER(LEN=7), PARAMETER :: CVIEW='pertcma'
LOGICAL :: LLCANARI

! Data
REAL(KIND=JPRD), ALLOCATABLE :: X(:,:)

! Random-related
INTEGER(KIND=JPIM),PARAMETER :: MAXOBS = 1000000
REAL(KIND=JPRB) :: ZGAUSS(MAXOBS)
INTEGER(KIND=JPIM) :: ISEED
TYPE(RANDOMNUMBERSTREAM) :: GOLUM

! Column indexes
INTEGER(KIND=JPIM) :: IERR,IOBS,IDEP,IUPDATEHR1,IUPDATEHR3

! Counts
INTEGER(KIND=JPIM) :: IDONE

! Auxiliary
REAL(KIND=JPRD) :: ZETA
CHARACTER(LEN=4) :: DBNAME
CHARACTER(LEN=60) :: CYARG,CYARG2
INTEGER(KIND=JPIM) :: IARGC,NARG

WRITE(*,*) 
WRITE(*,*) '*** PERTURBING CMA OBSERVATIONS ***'
WRITE(*,*) 

!--- GET COMMANDARGS
CYARG=' '
CALL GETARG(1,CYARG)
READ(CYARG,*) ISEED
CALL GETARG(0,CYARG)
DBNAME='CCMA'
NARG=IARGC()
WRITE(*,*) NARG

IF (NARG.GE.2) CALL GETARG(2,DBNAME)
IF (NARG.EQ.3) CALL GETARG(3,CYARG2)
IF(DBNAME(1:4).NE.'ECMA' .AND. DBNAME(1:4).NE.'CCMA')THEN
    WRITE(*,*) 'INVALID DBNAME',DBNAME(1:4)
    WRITE(*,*) 
    WRITE(*,*) 'Usage: ',TRIM(CYARG),' <seed> [ECMA | CCMA]'
    CALL ABORT
ELSE
    WRITE(*,*) 'DBNAME  : ',TRIM(DBNAME)
    WRITE(*,*) 'SQLVIEW : ',TRIM(CVIEW)
ENDIF

IF(CYARG2(1:6) .EQ. 'CANARI') THEN
   LLCANARI=.TRUE.
   WRITE(*,*) 'ASSUMING ECMA FOR CANARI'
ELSE 
   LLCANARI=.FALSE.
   WRITE(*,*) 'ASSUMING ECMA NOT FOR CANARI'
ENDIF

IF (ISEED.LT.0.AND.ISEED.GT.999) THEN
    WRITE(*,*) 'INVALID ISEED',ISEED
    WRITE(*,*) 
    WRITE(*,*) 'Usage: ',TRIM(CYARG),' <seed> [ECMA | CCMA]'
    CALL ABORT
ELSE
    WRITE(*,*) 'SEED for RANDOM generation:',ISEED
ENDIF

!--- INITIALIZE COLUMN INDEXES ACCORDING TO SQL QUERY
IOBS = 1
IDEP = 2
IERR = 3
IUPDATEHR1 = 4
IUPDATEHR3 = 5

!--- INITIALIZE RANDOM
CALL INITIALIZE_RANDOM_NUMBERS(ISEED,GOLUM)
CALL GAUSSIAN_DISTRIBUTION(ZGAUSS(1:MAXOBS),GOLUM)

!--- INITIALIZE AND OPEN DB
RC=ODB_INIT(NPROC=NPROC, MYPROC=MYPROC)
NPOOLS= 0
IDONE = 0
H=ODB_OPEN(DBNAME, 'OLD', NPOOLS=NPOOLS)

WRITE(*,*) 
cypool : DO JP=1,NPOOLS 

 RC=ODB_SELECT(H,CVIEW,NROWS,NCOLS,POOLNO=JP)
 WRITE(*,*) 'POOL:',JP,'  RECORDS:',NROWS
 ALLOCATE(X(NROWS,0:NCOLS))
 RC=ODB_GET(H,CVIEW,X,NROWS,NCOLS,POOLNO=JP)

 !--- CYCLE OVER RECORDS
 cyrec : DO JR=1,NROWS
    IDONE=IDONE+1
    IF (IDONE .LT.10) WRITE(71,*) (X(IDONE,JC),JC=1,NCOLS)
    IF(IDONE.GT.MAXOBS)THEN
      WRITE(*,*) 'MAXOBS TO INCREASE, THERE MORE OBS THAN',MAXOBS
      ! Try to restore
      RC=ODB_CANCEL(H,CVIEW,POOLNO=JP)
      RC=ODB_CLOSE(H, SAVE=.FALSE.)
      RC=ODB_END()
      CALL ABORT()
    ENDIF
    ! Get obs error and set perturbation accordingly
    ZETA=ZGAUSS(IDONE)*X(JR,IERR)
    ! Update obsvalue and first guess departure
    X(JR,IOBS)=X(JR,IOBS)+ZETA
    IF(.NOT. LLCANARI) &
    & X(JR,IDEP)=X(JR,IDEP)+ZETA
    ! Keeps track of perturbation
    IF(.NOT. LLCANARI) &
    & X(JR,IUPDATEHR1)=X(JR,IDEP)
    X(JR,IUPDATEHR3)=ZETA
 ENDDO cyrec

 !--- Put back values in ODB
 RC=ODB_PUT(H,CVIEW,X,NROWS,NCOLS,POOLNO=JP)

 !--- Prepare for next pool
 DEALLOCATE(X)
 RC=ODB_CANCEL(H,CVIEW,POOLNO=JP)

ENDDO cypool

!--- Bye
WRITE(*,*) 
WRITE(*,*) 'PERTURBED OBSERVATIONS:',IDONE
WRITE(*,*) 

RC=ODB_CLOSE(H, SAVE=.TRUE.)
RC=ODB_END()

END PROGRAM PERTCMA
