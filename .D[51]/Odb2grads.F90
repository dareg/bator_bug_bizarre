program Odb2grads

! Creates GrADS station data file & ctl-file from a specific ODB-request
!   generated by odb2grads-script

use odb_module
use mpl_module

implicit none

integer numargs, iflag, nlev, ivarno, jj, icount, byteswapped, offset
character(len=64) dbname, view, arg, colname
character(len=8) statid
integer nrows, ncols, nra, npools, rc, h, j, jp, nvars, usemdi, seqno, prev_seqno
real(4) lat,lon,tim
real(8), allocatable :: x(:,:), dval(:)
real(4), allocatable :: val(:)
integer, allocatable :: varno(:)
character(len=32), allocatable :: chvarno(:)
logical LLfirst, LLsample
real(8) z, rad2deg, press, prev_press, mdi
RAD2DEG(z) = 57.295779513082323d0 * z

rc = ODB_init()

numargs = MPL_iargc()
if (numargs >= 1) then
  call MPL_getarg(1,dbname)
else
  dbname = 'ECMA'
endif

if (numargs >= 2) then
  call MPL_getarg(2,view)
else
  view = 'odb2grads'
endif

if (numargs >= 3) then
  call MPL_getarg(3,arg)
  read(arg,*) nvars
else
  nvars = 0
endif
allocate(dval(nvars))
allocate(val(nvars))
allocate(varno(nvars))
allocate(chvarno(nvars))

if (numargs >= 4) then
  call MPL_getarg(4,arg)
  read(arg,*) usemdi
else
  usemdi = 0
endif

if (numargs >= 5) then
  call MPL_getarg(5,arg)
  read(arg,*) byteswapped
else
  byteswapped = 0
endif

if (numargs >= 6) then
  call MPL_getarg(6,colname)
else
  colname = 'obsvalue@body'
endif
offset = 6

h = ODB_open(dbname,'READONLY',npools=npools)
mdi = ODB_getval(h,'$mdi')
if (mdi == 0) mdi = 2147483647

varno(:) = 0
do j=offset+1,min(numargs,offset+nvars)
  call MPL_getarg(j,arg)
  if (arg(1:1) == '$') then
    jj = ODB_getval(h,trim(arg))
    chvarno(j-offset) = trim(arg(2:))
  else if (ichar(arg(1:1)) >= ichar('a') .and. ichar(arg(1:1)) <= ichar('z')) then
    jj = ODB_getval(h,'$'//trim(arg))
    chvarno(j-offset) = trim(arg(1:))
  else if (ichar(arg(1:1)) >= ichar('A') .and. ichar(arg(1:1)) <= ichar('Z')) then
    jj = ODB_getval(h,'$'//trim(arg))
    chvarno(j-offset) = trim(arg(1:))
  else
    read(arg,*) jj
    write(chvarno(j-offset),'(a,i3.3)') 'varno_',jj
  endif
  varno(j-offset) = jj
enddo

open(1,file=trim(view)//'.ctl',status='unknown')
write(1,'(a)') 'DSET   '//trim(view)//'.dat'
write(1,'(a)') 'DTYPE  station'
write(1,'(a)') 'STNMAP '//trim(view)//'.map'
write(1,'(a)') 'UNDEF -999.0'
write(1,'(a)') 'OPTIONS sequential'
if (byteswapped == 1) then
!-- The next one should applicable only if compiled with 
!   pgf90 -byteswapio or
!   ifort -convert big_endian 
!   G95_ENDIAN=big when running g95-linked program
  write(1,'(a)') 'OPTIONS byteswapped'
endif
write(1,'(a)') 'TITLE Observation coverage for '//trim(colname)
write(1,'(a)') 'TDEF 1 linear 00z01jan2000 1hr'
write(1,'(a,i5)') 'VARS ',nvars
do j=1,nvars
  jj = varno(j)
  write(1,'(a,i5)') trim(chvarno(j))//' 0 99 ODB-database '//trim(dbname)//' variable #',jj
enddo
write(1,'(a)') 'ENDVARS'
close(1)

open(2,file=trim(view)//'.dat',status='unknown',form='unformatted')

statid = ' '
lat = 0
lon = 0
tim = 0
nlev = 1
iflag = 1

do jp=1,npools
  rc = ODB_select(h,view,nrows,ncols,nra=nra,poolno=jp)
  write(0,*) jp,nrows,ncols

  prev_seqno = -1
  prev_press = -1
  LLfirst = .TRUE.
  LLsample = .TRUE.
  dval(:) = abs(mdi)
  icount = 0

  if (nrows > 0) then
    allocate(x(nra,0:ncols))
    rc = ODB_get(h,view,x,nrows,ncols,poolno=jp)
    do j=1,nrows
      seqno = x(j,4)
      press = x(j,5)
      if (.not.LLfirst .and. (seqno /= prev_seqno .or. press /= prev_press)) then
        where (abs(dval(:)) == abs(mdi))
          val(:) = -999.0
        elsewhere
          val(:) = dval(:)
        endwhere
        write(2) statid,lat,lon,tim,nlev,iflag
        write(2) val(:)
        if (LLsample) then
          write(0,'(a,2f10.3,f7.1,2i3,1p,5g20.12)') statid,lat,lon,tim,nlev,iflag,val(:)
          LLsample = .FALSE.
        endif
        dval(:) = abs(mdi)
        icount = 0
      endif
      prev_press = press
      prev_seqno = seqno
      statid = transfer(x(j,1),statid)
      lat = rad2deg(x(j,2))
      lon = rad2deg(x(j,3))
      ivarno = x(j,6)
      do jj=1,nvars
        if (ivarno == varno(jj)) then
          dval(jj) = x(j,7)
          exit
        endif
      enddo
      icount = icount + 1
      if (j==1) LLfirst = .FALSE.
    enddo
    deallocate(x)
  endif

  if (icount > 0) then
    where (abs(dval(:)) == abs(mdi))
      val(:) = -999.0
    elsewhere
      val(:) = dval(:)
    endwhere
    write(2) statid,lat,lon,tim,nlev,iflag
    write(2) val(:)
    if (LLsample) then
      write(0,'(a,2f10.3,f7.1,2i3,1p,5g20.12)') statid,lat,lon,tim,nlev,iflag,val(:)
    endif
  endif
  rc = ODB_cancel(h,view,poolno=jp)
  rc = ODB_release(h,poolno=jp)
enddo

nlev = 0
write(2) statid,lat,lon,tim,nlev,iflag
close(2)

rc = ODB_close(h)

rc = ODB_end()
end program Odb2grads
